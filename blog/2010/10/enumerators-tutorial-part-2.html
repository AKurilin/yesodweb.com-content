<p>This content is now part of the <a href="http://www.yesodweb.com/book/enumerator">Yesod book</a>. It is recommended to read there, since the content is more up-to-date.</p>
<p>Note: code for this tutorial is available as <a href="http://gist.github.com/607856">a github gist</a>.</p>
<h2 id="extracting-a-value">Extracting a value</h2>
<p>When we finished the <a href="http://docs.yesodweb.com/blog/enumerators-tutorial-part-1/">last part of our tutorial</a>, we had written a few iteratees, but we still didn't know how to extract values from them. To start, let's remember that Iteratee is just a newtype wrapper around Step:</p>
<pre><code>newtype Iteratee a m b = Iteratee { runIteratee :: m (Step a m b) }
</code></pre>
<p>First we need to unwrap the Iteratee and deal with the Step value inside. Remember also that Step has three constructors: Continue, Yield and Error. We'll handle the Error constructor by returning our result in an Either. Yield already provides the data we're looking for.</p>
<p>The tricky case is Continue: here, we have an iteratee that is still expecting more data. This is where the EOF constructor comes in handy: it's our little way to tell the iteratee to finish what it's doing and get on with things. If you remember from the last part, I said a well-behaving iteratee will never return a Continue after receiving an EOF; now we'll see why:</p>
<pre><code>extract :: Monad m =&gt; Iteratee a m b -&gt; m (Either SomeException b)
extract (Iteratee mstep) = do
    step &lt;- mstep
    case step of
        Continue k -&gt; do
            let Iteratee mstep' = k EOF
            step' &lt;- mstep'
            case step' of
                Continue _ -&gt; error &quot;Misbehaving iteratee&quot;
                Yield b _ -&gt; return $ Right b
                Error e -&gt; return $ Left e
        Yield b _ -&gt; return $ Right b
        Error e -&gt; return $ Left e
</code></pre>
<p>Fortunately, you don't need to redefine this yourself: enumerator includes both a <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:run">run</a> and <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:run_">run_</a> function. Let's go ahead and use it on our sum6 function:</p>
<pre><code>main = run_ sum6 &gt;&gt;= print
</code></pre>
<p>If you run this, the result will be 0. This emphasizes an important point: an iteratee is not just <em>how</em> to process incoming data, <strong>it is the state of the processing</strong>. In this case, we haven't done anything to change the initial state of sum6, so we still have the initial value of 0.</p>
<p>To give an analogy: think of an iteratee as a machine. When you feed it data, you modify the internal state but you can't see any of those changes on the outside. When you are done feeding the data, you press a button and it spits out the result. If you don't feed in any data, your result is the initial state.</p>
<h2 id="adding-data">Adding data</h2>
<p>Let's say that we actually want to sum some numbers. For example, the numbers 1 to 10. We need some way to feed that into our sum6 iteratee. In order to approach this, we'll once again need to unwrap our Iteratee and deal with the Step value directly.</p>
<p>In our case, we know with certainty that the Step constructor we used is Continue, so it's safe to write our function as:</p>
<pre><code>sum7 :: Monad m =&gt; Iteratee Int m Int
sum7 = Iteratee $ do
    Continue k &lt;- runIteratee sum6
    runIteratee $ k $ Chunks [1..10]
</code></pre>
<p>But in general, we won't know what constructor will be lying in wait for us. We need to properly deal with Continue, Yield and Error. We've seen what to do with Continue: feed it the data. With Yield and Error, the right action in general is to <strong>do nothing</strong>, since we've already arrived at our final result (either a successful Yield or an Error). So the &quot;proper&quot; way to write the above function is:</p>
<pre><code>sum8 :: Monad m =&gt; Iteratee Int m Int
sum8 = Iteratee $ do
    step &lt;- runIteratee sum6
    case step of
        Continue k -&gt; runIteratee $ k $ Chunks [1..10]
        _ -&gt; return step
</code></pre>
<h2 id="enumerator-type-synonym">Enumerator type synonym</h2>
<p>What we've done with sum7 and sum8 is perform a transformation on the Iteratee. But we've done this in a very limited way: we've hard-coded in the original Iteratee function (sum6). We could just make this an argument to the function:</p>
<pre><code>sum9 :: Monad m =&gt; Iteratee Int m Int -&gt; Iteratee Int m Int
sum9 orig = Iteratee $ do
    step &lt;- runIteratee orig
    case step of
        Continue k -&gt; runIteratee $ k $ Chunks [1..10]
        _ -&gt; return step
</code></pre>
<p>But since we always just want to unwrap the Iteratee value anyway, it turns out that it's more natural to make the argument of type Step, ie:</p>
<pre><code>sum10 :: Monad m =&gt; Step Int m Int -&gt; Iteratee Int m Int
sum10 (Continue k) = k $ Chunks [1..10]
sum10 step = returnI step
</code></pre>
<p>This type signature (take a Step, return an Iteratee) turns out to be very common:</p>
<pre><code>type Enumerator a m b = Step a m b -&gt; Iteratee a m b
</code></pre>
<p>Meaning sum10's type signature could also be expressed as:</p>
<pre><code>sum10 :: Monad m =&gt; Enumerator Int m Int
</code></pre>
<p>Of course, we need some helper function to connect an Enumerator and an Iteratee:</p>
<pre><code>applyEnum :: Monad m =&gt; Enumerator a m b -&gt; Iteratee a m b -&gt; Iteratee a m b
applyEnum enum iter = Iteratee $ do
    step &lt;- runIteratee iter
    runIteratee $ enum step
</code></pre>
<p>Let me repeat the intuition here: the Enumerator is transforming the Iteratee from its initial state to a new state by feeding it more data. In order to use this function, we could write:</p>
<pre><code>run_ (applyEnum sum10 sum6) &gt;&gt;= print
</code></pre>
<p>This results in 55, exactly as we'd expect. But now we can see one of the benefits of enumerators: we can use multiple data sources. Let's say we have another enumerator:</p>
<pre><code>sum11 :: Monad m =&gt; Enumerator Int m Int
sum11 (Continue k) = k $ Chunks [11..20]
sum11 step = returnI step
</code></pre>
<p>Then we could simply apply both enumerators:</p>
<pre><code>run_ (applyEnum sum11 $ applyEnum sum10 sum6) &gt;&gt;= print
</code></pre>
<p>And we would get the result 210. (Yes, (1 + 20) * 10 = 210.) But don't worry, you don't need to write this applyEnum function yourself: enumerator provides a <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:-36--36-">$$</a> operator which does the same thing. Its type signature is a bit scarier, since it's a generalization of applyEnum, but it works the same, and even makes code more readable:</p>
<pre><code>run_ (sum11 $$ sum10 $$ sum6) &gt;&gt;= print
</code></pre>
<p><code>$$</code> is a synonym for <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:-61--61--60--60-">==&lt;&lt;</a>, which is simply <code>flip <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:-62--62--61--61-">&gt;&gt;==</a></code>. I find <code>$$</code> the most readable, but <abbr title="your mileage my vary">YMMV</abbr>.</p>
<h2 id="some-built-in-enumerators">Some built-in enumerators</h2>
<p>Of course, writing a whole function just to pass some numbers to our sum function seems a bit tedious. We could easily make the list an argument to the function:</p>
<pre><code>sum12 :: Monad m =&gt; [Int] -&gt; Enumerator Int m Int
sum12 nums (Continue k) = k $ Chunks nums
sum12 _ step = returnI step
</code></pre>
<p>But now there's not even anything Int-specific in our function. We could easily generalize this to:</p>
<pre><code>genericSum12 :: Monad m =&gt; [a] -&gt; Enumerator a m b
genericSum12 nums (Continue k) = k $ Chunks nums
genericSum12 _ step = returnI step
</code></pre>
<p>And in fact, enumerator comes built in with the <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:enumList">enumList</a> function which does this. enumList also takes an Integer argument to indicate the maximum number of elements to stick in a chunk. For example, we could write:</p>
<pre><code>run_ (enumList 5 [1..30] $$ sum6) &gt;&gt;= print
</code></pre>
<p>(That produces 465 if you're counting.) The first argument to enumList should never affect the result, though it may have some performance impact.</p>
<p>Data.Enumerator includes two other enumerators: <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:enumEOF">enumEOF</a> simply passes an EOF to the iteratee. <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:concatEnums">concatEnums</a> is slightly more interesting; it combines multiple enumerators together. For example:</p>
<pre><code>run_ (concatEnums
        [ enumList 1 [1..10]
        , enumList 1 [11..20]
        , enumList 1 [21..30]
        ] $$ sum6) &gt;&gt;= print
</code></pre>
<p>This also produces 465.</p>
<h2 id="some-non-pure-input">Some non-pure input</h2>
<p>Enumerators are much more interesting when they aren't simply dealing with pure values. In the first part of this tutorial, we gave the example of the user entering numbers on the command line:</p>
<pre><code>getNumber :: IO (Maybe Int)
getNumber = do
    x &lt;- getLine
    if x == &quot;q&quot;
        then return Nothing
        else return $ Just $ read x

sum2 :: IO Int
sum2 = do
    maybeNum &lt;- getNumber
    case maybeNum of
        Nothing -&gt; return 0
        Just num -&gt; do
            rest &lt;- sum2
            return $ num + rest
</code></pre>
<p>We referred to this as the pull-model: sum2 pulled each value from getNumber. Let's see if we can rewrite getNumber to be a pusher instead of a pullee.</p>
<pre><code>getNumberEnum :: MonadIO m =&gt; Enumerator Int m b
getNumberEnum (Continue k) = do
    x &lt;- liftIO getLine
    if x == &quot;q&quot;
        then continue k
        else k (Chunks [read x]) &gt;&gt;== getNumberEnum
getNumberEnum step = returnI step
</code></pre>
<p>First, notice that we check which constructor was passed, and only perform any actions if it was Continue. If it was Continue, we get the line of input from the user. If the line is &quot;q&quot; (our indication to stop feeding in values), we do nothing. You <em>might</em> have thought that we should pass an EOF. But if we did that, we'd be preventing other data from being sent to this iteratee. Instead, we simply return the original Step value.</p>
<p>If the line was not &quot;q&quot;, we convert it to an Int via read, create a Stream value with the Chunks datatype, and pass it to k. (If we wanted to do things properly, we'd check if x is really an Int and use the Error constructor; I leave that as an exercise to the reader.) At this point, let's look at type signatures:</p>
<pre><code>k (Chunks [read x]) :: Iteratee Int m b
</code></pre>
<p>If we simply left off the rest of the line, our program would typecheck. However, it would only ever read one value from the command line; the <code>&gt;&gt;== getNumberEnum</code> causes our enumerator to loop.</p>
<p>One last thing to note about our function: notice the b in our type signature.</p>
<pre><code>getNumberEnum :: MonadIO m =&gt; Enumerator Int m b
</code></pre>
<p>This is saying that our Enumerator can feed <code>Int</code>s to any Iteratee accepting <code>Int</code>s, and it doesn't matter what the final output type will be. This is in general the way enumerators work. This allows us to create drastically different iteratees that work with the same enumerators:</p>
<pre><code>intsToStrings :: (Show a, Monad m) =&gt; Iteratee a m String
intsToStrings = (unlines . map show) `fmap` consume
</code></pre>
<p>And then both of these lines work:</p>
<pre><code>run_ (getNumberEnum $$ sum6) &gt;&gt;= print
run_ (getNumberEnum $$ intsToStrings) &gt;&gt;= print
</code></pre>
<h2 id="exercises">Exercises</h2>
<ul><li><p>Write an enumerator that reads lines from stdin (as Strings). Make sure it works with this iteratee:</p>
<pre>printStrings :: Iteratee String IO ()
printStrings = do
    mstring <- head="" case="" mstring="" of="" Nothing="" -="">return ()
        Just string -> do
            liftIO $ putStrLn string
            printStrings</pre>
</li>
<li><p>Write an enumerator that does the same as above with words (ie, delimit on any whitespace). It should work with the same Iteratee as above.</p></li>
<li><p>Do proper error handling in the getNumberEnum function above when the string is not a proper integer.</p></li>
<li><p>Modify getNumberEnum to pull its input from a file instead of stdin.</p></li>
<li><p>Use your modified getNumberEnum to sum up the values in two different files.</p></li>
</ul>
<h2 id="summary">Summary</h2>
<ul><li><p>An enumerator is a <strong>step transformer</strong>: it feeds data into an iteratee to produce a new iteratee with an updated state.</p></li>
<li><p>Multiple enumerators can be fed into a single iteratee, and we finally use the run and run_ functions to extract results.</p></li>
<li><p>We can use the $$, &gt;&gt;== and ==&lt;&lt; operators to apply an enumerator to an iteratee.</p></li>
<li><p>When writing an enumerator, we only feed data to an iteratee in the Continue state; Yield and Error already represent final values.</p></li>
</ul>