<p>This content is now part of the <a href="http://www.yesodweb.com/book/enumerator">Yesod book</a>. It is recommended to read there, since the content is more up-to-date.</p>
<h2 id="introduction">Introduction</h2>
<p>One of the upcoming patterns in Haskell is the enumerators. Unfortunately, it's very difficult to get started with them since:</p>
<ul><li><p>There are multiple implementations, all with slightly different approaches.</p></li>
<li><p>Some of the implementations (in my opinion) use incredibly confusing naming.</p></li>
<li><p>The tutorials that get written usually don't directly target an existing implementation, and work more on building up intuition than giving instructions on how to use the library.</p></li>
</ul>
<p>I'm hoping that this tutorial will fill the gap a bit. I'm going to be basing this on the <a href="http://hackage.haskell.org/package/enumerator">enumerator package</a>. I'm using version 0.4.0.2, but this should be applicable to older and hopefully newer versions as well. This package is newer and less used than the <a href="http://hackage.haskell.org/package/iteratee">iteratee</a> package, but I've chosen it for three reasons:</p>
<ul><li><p>It has a much smaller dependency list.</p></li>
<li><p>It's a smaller package, and therefore easier to wrap your mind around.</p></li>
<li><p>I think the naming is better.</p></li>
</ul>
<p>That said, both packages are built around the same basic principles, so learning one will definitely help you with the other.</p>
<h3 id="three-parts">Three Parts</h3>
<p>The title of this post says this is part 1. In theory, there will be three parts (though I may do more or less, I'm not certain yet). There are really three main concepts to learn to use the enumerator package: iteratees, enumerators and enumeratees. A basic definition would be:</p>
<ul><li><p>Iteratees are <em>consumers</em>: they are fed data and do something with it.</p></li>
<li><p>Enumerators are <em>producers</em>: they feed data to an iteratee.</p></li>
<li><p>Enumeratees are <em>pipes</em>: they are fed data from an enumerator and then feed it to an iteratee.</p></li>
</ul>
<h3 id="what-good-are-enumerators">What good are enumerators?</h3>
<p>But before you really get into this library, let's give some motivation for <em>why</em> we would want to use it. Here's some real life examples I use the enumerator package for:</p>
<ul><li><p>When reading values from a database, I don't necessarily want to pull all records into memory at once. Instead, I would like to have them fed to a function which will consume them bit by bit.</p></li>
<li><p>When processing a YAML file, instead of reading in the whole structure, maybe you only need to grab the value of one or two records.</p></li>
<li><p>If you want to download a file via HTTP and save the results in a file, it would be a waste of RAM to store the whole file in memory and then write it out. Enumerators let you perform interleaved IO actions easily.</p></li>
</ul>
<p>A lot of these problems can also be solved using lazy I/O. However, lazy I/O is not necessarily a panacea: you might want to read some of <a href="http://okmij.org/ftp/Streams.html#iteratee">Oleg's stuff</a> on the pitfalls of lazy I/O.</p>
<h2 id="intuition">Intuition</h2>
<p><em>Note: you can see the code in this post as a <a href="http://gist.github.com/605826">github gist</a>.</em></p>
<p>Let's say we want to write a function that sums the numbers in a list. Forgetting uninteresting details like space leaks, a perfectly good implementation could be:</p>
<pre><code>sum1 :: [Int] -&gt; Int
sum1 [] = 0
sum1 (x:xs) = x + sum1 xs
</code></pre>
<p>But let's say that we don't have a list of numbers. Instead, the user is typing numbers on the command line, and hitting &quot;q&quot; when done. In other words, we have a function like:</p>
<pre><code>getNumber :: IO (Maybe Int)
getNumber = do
    x &lt;- readLine
    if x == &quot;q&quot;
        then return Nothing
        else return $ Just $ read x
</code></pre>
<p>We could write our new sum function as:</p>
<pre><code>sum2 :: IO Int
sum2 = do
    maybeNum &lt;- getNumber
    case maybeNum of
        Nothing -&gt; return 0
        Just num -&gt; do
            rest &lt;- sum2
            return $ num + rest
</code></pre>
<p>It's fairly annoying to have to write two completely separate sum functions just because our data source changed. Ideally, we would like to generalize things a bit. Let's start by noticing a similarity between these two functions: they both only <strong>yield</strong> a value when they are informed that there are no more numbers. In the case of sum1, we check for an empty list; in sum2, we check for Nothing.</p>
<h2 id="the-stream-datatype.">The Stream datatype.</h2>
<p>The first datatype defined in the enumerator package is:</p>
<pre><code>data Stream a = Chunks [a] | EOF
</code></pre>
<p>The EOF constructor indicates that no more data is available. The Chunks constructor simply allows us to put multiple pieces of data together for efficiency. We could now rewrite sum2 to use this Stream datatype:</p>
<pre><code>getNumber2 :: IO (Stream Int)
getNumber2 = do
    maybeNum &lt;- getNumber -- using the original getNumber function
    case maybeNum of
        Nothing -&gt; return EOF
        Just num -&gt; return $ Chunks [num]

sum3 :: IO Int
sum3 = do
    stream &lt;- getNumber2
    case stream of
        EOF -&gt; return 0
        Chunks nums -&gt; do
            let nums' = sum nums
            rest &lt;- sum3
            return $ nums' + rest
</code></pre>
<p>Not that it's much better than sum2, but at least it shows how to use the Stream datatype. The problem here is that we still refer explicitly to the getNumber2 function, hard-coding the data source.</p>
<p>One possible solution is to make the data source an argument to the sum function, ie:</p>
<pre><code>sum4 :: IO (Stream Int) -&gt; IO Int
sum4 getNum = do
    stream &lt;- getNum
    case stream of
        EOF -&gt; return 0
        Chunks nums -&gt; do
            let nums' = sum nums
            rest &lt;- sum4 getNum
            return $ nums' + rest
</code></pre>
<p>That's all well and good, but let's pretend we want to have <em>two</em> datasources to sum over: values the user enters on the command line, and some numbers we read over an HTTP connection, perhaps. The problem here is one of <strong>control</strong>: sum4 is running the show here by calling getNum. This is a <strong>pull</strong> data model. Enumerators have an <strong>inversion of control/push</strong> model, putting the enumerator in charge. This allows cool things like feeding in multiple data sources, and also makes it easier to write enumerators that properly deal with resource allocation.</p>
<h2 id="the-step-datatype">The Step datatype</h2>
<p>So we need a new datatype that will represent the state of our summing operation. We're going to allow our operations to be in one of three states:</p>
<ul><li><p>Waiting for more data.</p></li>
<li><p>Already calculated a result.</p></li>
<li><p>For convenience, we also have an error state. This isn't strictly necessary (it could be modeled by choosing an EitherT kind of monad, for example), but it's simpler.</p></li>
</ul>
<p>As you could guess, these states will correspond to three constructors for the Step datatype. The error state is modeled by <code>Error SomeException</code>, building on top of Haskell's extensible exception system. The already calculated constructor is:</p>
<pre><code>Yield b (Stream a)
</code></pre>
<p>Here, a is the <em>input</em> to our iteratee and b is the <em>output</em>. This constructor allows us to simultaneously produce a result and save any &quot;leftover&quot; input for another iteratee that may run after us. (This won't be the case with the sum function, which always consumes all its input, but we'll see some other examples that do no such thing.)</p>
<p>Now the question is how to represent the state of an iteratee that's waiting for more data. You might at first want to declare some datatype to represent the internal state and pass that around somehow. That's not how it works: instead, we simply use a function (very Haskell of us, right?):</p>
<pre><code>Continue (Stream a -&gt; Iteratee a m b)
</code></pre>
<p>Euerka! We've finally seen the Iteratee datatype! Actually, Iteratee is a very boring datatype that is only present to allow us to declare cool instances (eg, Monad) for our functions. Iteratee is defined as:</p>
<pre><code>newtype Iteratee a m b = Iteratee (m (Step a m b))
</code></pre>
<p>This is important: <strong>Iteratee is just a newtype wrapper around a Step inside a monad</strong>. Just keep that in mind as you look at definitions in the enumerator package. So knowing this, we can think of the Continue constructor as:</p>
<pre><code>Continue (Stream a -&gt; m (Step a m b))
</code></pre>
<p>That's much easier to approach: that function takes some input data and returns a new state of the iteratee. Let's see what our sum function would look like using this Step datatype:</p>
<pre><code>sum5 :: Monad m =&gt; Step Int m Int -- Int input, any monad, Int output
sum5 =
    Continue $ go 0 -- a common pattern, you always start with a Continue
  where
    go :: Monad m =&gt; Int -&gt; Stream Int -&gt; Iteratee Int m Int
    -- Add the new input to the running sum and create a new Continue
    go runningSum (Chunks nums) = do
        let runningSum' = runningSum + sum nums
        -- This next line is *ugly*, good thing there are some helper
        -- functions to clean it up. More on that below.
        Iteratee $ return $ Continue $ go runningSum'
    -- Produce the final result
    go runningSum EOF = Iteratee $ return $ Yield runningSum EOF
</code></pre>
<p>The first real line (<code>Continue $ go 0</code>) initializes our iteratee to its starting state. Just like every other sum function, we need to explicitly state that we are starting from 0 somewhere. The real workhorse is the go function. Notice how we are really passing the state of the iteratee around as the first argument to go: this is also a very common pattern in iteratees.</p>
<p>We need to handle two different cases: when handed an EOF, the go function <strong>must</strong> Yield a value. (Well, it could also produce an Error value, but it definitely <strong>cannot</strong> Continue.) In that case, we simply yield the running sum and say there was no data left over. When we receive some input data via Chunks, we simply add it to the running sum and create a new Continue based on the same go function.</p>
<p>Now let's work on making that function a little bit prettier by using some built-in helper functions. The pattern <code>Iteratee . return</code> is common enough to warrant a helper function, namely:</p>
<pre><code>returnI :: Monad m =&gt; Step a m b -&gt; Iteratee a m b
returnI = Iteratee . return
</code></pre>
<p>So for example,</p>
<pre><code>go runningSum EOF = Iteratee $ return $ Yield runningSum EOF
</code></pre>
<p>becomes</p>
<pre><code>go runningSum EOF = returnI $ Yield runningSum EOF
</code></pre>
<p>But even <em>that</em> is common enough to warrant a helper function:</p>
<pre><code>yield :: Monad m =&gt; b -&gt; Stream a -&gt; Iteratee a m b
yield x chunk = returnI $ Yield x chunk
</code></pre>
<p>so our line becomes</p>
<pre><code>go runningSum EOF = yield runningSum EOF
</code></pre>
<p>Similarly,</p>
<pre><code>Iteratee $ return $ Continue $ go runningSum'
</code></pre>
<p>becomes</p>
<pre><code>continue $ go runningSum'
</code></pre>
<h2 id="monad-instance-for-iteratee">Monad instance for Iteratee</h2>
<p>This is all very nice: we now have an iteratee that can be fed numbers from any monad and sum them. It can even take input from different sources and sum them together. (By the way, I haven't actually shown you how to feed those numbers in: that is in part 2 about enumerators.) But let's be honest: sum5 is an ugly function. Isn't there something easier?</p>
<p>In fact, there is. Remember how I said Iteratee really just existed to facilitate typeclass instances? This includes a monad instance. Feel free to look at the code to see how that instance is defined, but here we'll just look at how to use it:</p>
<pre><code>sum6 :: Monad m =&gt; Iteratee Int m Int
sum6 = do
    maybeNum &lt;- head -- not head from Prelude!
    case maybeNum of
        Nothing -&gt; return 0
        Just i -&gt; do
            rest &lt;- sum6
            return $ i + rest
</code></pre>
<p>That head function is not from Prelude, it's from the Data.Enumerator module. Its type signature is:</p>
<pre><code>head :: Monad m =&gt; Iteratee a m (Maybe a)
</code></pre>
<p>which basically means give me the next piece of input if it's there. We'll look at this function in more depth in a bit.</p>
<p>Go compare the code for sum6 with sum2: they are amazingly similar. You can often build up more complicated iteratees by using some simple iteratees and the Monad instance of Iteratee.</p>
<h2 id="interleaved-io">Interleaved I/O</h2>
<p>Alright, let's look at a totally different problem. We want to be fed some strings and print them to the screen one line at a time. One approach would be to use lazy I/O:</p>
<pre><code>lazyIO :: IO ()
lazyIO = do
    s &lt;- lines `fmap` getContents
    mapM_ putStrLn s
</code></pre>
<p>But this has two drawbacks:</p>
<ul><li><p>It's tied down to a single input source, stdin. This could be worked around with an argument giving a datasource.</p></li>
<li><p>But let's say the data source is some scarce resource (think: file handles on a very busy web server). We have no guarantees with lazy I/O of when those file handles will be released.</p></li>
</ul>
<p>Let's look at how to write this in our new high-level monadic iteratee approach:</p>
<pre><code>interleaved :: MonadIO m =&gt; Iteratee String m ()
interleaved = do
    maybeLine &lt;- head
    case maybeLine of
        Nothing -&gt; return ()
        Just line -&gt; do
            liftIO $ putStrLn line
            interleaved
</code></pre>
<p>The liftIO function comes from the transformers package, and simply promotes an action in the IO monad to any arbitrary MonadIO action. Notice how we don't really track any state with this iteratee: we don't care about its result, only its side effects.</p>
<h2 id="implementing-head">Implementing head</h2>
<p>As a last example, let's actually implement the head function.</p>
<pre><code>head' :: Monad m =&gt; Iteratee a m (Maybe a)
head' =
    continue go
  where
    go (Chunks []) = continue go
    go (Chunks (x:xs)) = yield (Just x) (Chunks xs)
    go EOF = yield Nothing EOF
</code></pre>
<p>Like our sum6 function, this also wraps an inner &quot;go&quot; function with a continue. However, we now have <em>three</em> clauses for our go function. The first handles the case of <code>Chunks []</code>. To quote the enumerator docs:</p>
<blockquote><p>(Chunks []) is used to indicate that a stream is still active, but currently has no available data. Iteratees should ignore empty chunks.</p>
</blockquote>
<p>The second clause handles the case where we are given some data. In this case, we yield the first element in the list, and return the rest as leftover data. The third clause handles the end of input by returning Nothing.</p>
<h2 id="exercises">Exercises</h2>
<ul><li><p>Rewrite sum6 using <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4/doc/html/Data-Enumerator.html#v:liftFoldL-39-">liftFoldL'</a>.</p></li>
<li><p>Implement the <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4/doc/html/Data-Enumerator.html#v:consume">consume</a> function using first the high-level functions like head, and then using only low-level stuff.</p></li>
<li><p>Write a modified version of consume that only keeps every other value, once again using high-level functions and then low-level constructors.</p></li>
</ul>
<h2 id="next-time">Next time</h2>
<p>Well, now you can write iteratees, but they're not very useful if you can't actually use them. Next time we'll cover what an enumerator is, some basic enumerators included with the package, how to run these things and how to write your own enumerator.</p>
<h2 id="summary">Summary</h2>
<p>Here's what I consider the most important things to glean from this tutorial:</p>
<ul><li><p>Iteratee is a simple wrapper around the Step datatype to allow for cool typeclass instances.</p></li>
<li><p>Using the Monad instance of Iteratee can allow you to build up complicated iteratees from simpler ones.</p></li>
<li><p>The three states an enumerator can be in are Continue (still processing data), Yield (a result is ready) and Error (duh).</p></li>
<li><p>Well behaved iteratees will never return a Continue after receiving an EOF.</p></li>
</ul>