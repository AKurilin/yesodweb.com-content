<?xml version="1.0" encoding="UTF-8"?>
<html><h1>Sessions</h1><p id="i1" class="hascomments">HTTP is a stateless protocol. While some view this as a disadvantage, advocates of
            RESTful web development laud this as an plus. When state is removed from the picture, it
            is easier to scale applications, caching can happen automatically, and many other nice
            side effects occur. You can draw many parallels with the non-mutable nature of Haskell
            in general.</p>
        <p id="x-3" class="hascomments">As much as possible, RESTful applications should avoid storing state about an interaction
            with a client. However, it is sometimes unavoidable. Features like shopping carts are
            the classic example, but other more mundane interactions like proper login handling can
            be greatly enhanced by proper usage of sessions.</p><p id="i2" class="hascomments">This chapter will describe how Yesod stores session data,
            how you can access this data, and some special functions to help
            you make the most of sessions.</p><h2>Clientsession</h2><p id="cs1" class="hascomments">One of the earliest packages spun off from Yesod was clientsession. This package uses encryption and signatures to store data
            in a client-side cookie. The encryption prevents the user from inspecting the data, and
            the signature ensures that the session can be neither hijacked nor tampered with.</p><p id="cs2" class="hascomments">It might sound like a bad idea from an efficiency
            standpoint to store data in a cookie: after all, this means that
            the data must be sent on every request. However, in practice,
            clientsession can be a great boon for performance.</p><ul id="x2"><li id="x3">No server side database lookup is required to service a request.</li><li id="x4">We can easily scale horizontally: each request contains all the information we need to send a response.</li><li id="x5">To avoid undue bandwidth overhead, production sites can serve their static content from
                a separate domain name to avoid the overhead of transmitting the session cookie for
                each request.</li></ul><p id="cs3" class="hascomments">Storing megabytes of information in the session will be a bad idea. But for that matter,
            most session implementations recommend against such practices. If you really need
            massive storage for a user, it is best to store a lookup key in the session, and put the
            actual data in a database.</p>
        <p id="x-3" class="hascomments">All of the interaction with clientsession is handled by Yesod internally, but there are a
            few spots where you can tweak the behavior just a bit.</p><h2>Controlling sessions</h2><p id="c1" class="hascomments">There are three functions in the Yesod typeclass that control how sessions work.
                yesod-core:Yesod.Core:encryptKey returns the encryption key used.
            By default, it will take this from a local file, so that sessions can persist between
            database shutdowns. This file will be automatically created and filled with random data
            if it does not exist. And if you override this function to return Nothing, sessions will be disabled.</p><aside id="x3" class="note"><p id="c2" class="hascomments">Why disable sessions? They do introduce a
                performance overhead. Under normal circumstances, this overhead
                is minimal, especially compared to database access. However,
                when dealing with very basic tasks, the overhead can become
                noticeable. But be careful about disabling sessions: this will
                also disable such features as 
                    CSRF (Cross-Site Request Forgery) protection.</p></aside><p id="c3" class="hascomments">The next function is yesod-core:Yesod.Core:clientSessionDuration. This function gives the
            number of minutes that a session should be active. The default is 120 (2 hours).</p><p id="c4" class="hascomments">This value ends up affecting the session cookie in two ways:
            firstly, it determines the expiration date for the cookie itself.
            More importantly, however, the session expiration timestamp is
            encoded inside the session signature. When Yesod decodes the
            signature, it checks if the date is in the past; if so, it ignores
            the session values.</p><aside id="x6" class="note"><p id="c5" class="hascomments">Every time Yesod sends a response to the client, it
                sends an updated session cookie with a new expire date. This
                way, even if you do not change the session values themselves, a
                session will not time out if the user continues to browse your
                site.</p></aside><p id="c6" class="hascomments">And this leads very nicely to the last function: yesod-core:Yesod.Core:sessionIpAddress. By default, Yesod also encodes
            the client's IP address inside the cookie to prevent session hijacking. In general, this
            is a good thing. However, some ISPs are known for putting their users behind proxies
            that rewrite their IP addresses, sometimes changing the source IP in the middle of the
            session. If this happens, and you have sessionIpAddress enabled, the
            user's session will be reset. Turning this setting to False will allow
            a session to continue under such circumstances, at the cost of exposing a user to
            session hijacking.</p><h2>Session Operations</h2><p id="o1" class="hascomments">Like most frameworks, a session in Yesod is a key-value store. The base session API boils
            down to four functions: lookupSession gets a value for a key
            (if available), getSession returns all of the key/value pairs, setSession sets a value for a key, and deleteSession clears a value for a key.</p><pre id="x4"><code>{-# LANGUAGE TypeFamilies, QuasiQuotes, TemplateHaskell, MultiParamTypeClasses, OverloadedStrings #-}
import Yesod
import Control.Applicative ((&lt;$&gt;), (&lt;*&gt;))

data SessionExample = SessionExample

mkYesod "SessionExample" [parseRoutes|
/ Root GET POST
|]

getRoot :: Handler RepHtml
getRoot = do
    sess &lt;- getSession
    hamletToRepHtml [hamlet|
&lt;form method=post&gt;
    &lt;input type=text name=key&gt;
    &lt;input type=text name=val&gt;
    &lt;input type=submit&gt;
&lt;h1&gt;#{show sess}
|]

postRoot :: Handler ()
postRoot = do
    (key, mval) &lt;- runInputPost $ (,) &lt;$&gt; ireq textField "key" &lt;*&gt; iopt textField "val"
    case mval of
        Nothing -&gt; deleteSession key
        Just val -&gt; setSession key val
    liftIO $ print (key, mval)
    redirect Root

instance Yesod SessionExample where
    clientSessionDuration _ = 1

instance RenderMessage SessionExample FormMessage where
    renderMessage _ _ = defaultFormMessage

main :: IO ()
main = warpDebug 3000 SessionExample</code></pre><h2>Messages</h2><p id="m1" class="hascomments">One usage of sessions previously alluded to is messages. They come to solve a common
            problem in web development: the user performs a POST request, the web
            app makes a change, and then the web app wants to simultaneously redirect
            the user to a new page and send the user a success message. (This is known as
            Post/Redirect/Get.)</p><p id="m2" class="hascomments">Yesod provides a pair of functions to make this very easy:
            setMessage stores a value in the session, and
            getMessage both reads the value most recently put into
            the session, and clears the old value so it does not accidently get
            displayed twice.</p><p id="m3" class="hascomments">It is recommended to have a call to getMessage in defaultLayout so that any available message is shown to a user
            immediately, without having to remember to add getMessage calls to
            every handler.</p>
        <pre id="x-3"><code>{-# LANGUAGE OverloadedStrings, TypeFamilies, TemplateHaskell,
             QuasiQuotes, MultiParamTypeClasses #-}
import Yesod

data Messages = Messages

mkYesod "Messages" [parseRoutes|
/ RootR GET
/set-message SetMessageR POST
|]

instance Yesod Messages where
    defaultLayout widget = do
        pc &lt;- widgetToPageContent widget
        mmsg &lt;- getMessage
        hamletToRepHtml [hamlet|
$doctype 5
&lt;html&gt;
    &lt;head&gt;
        &lt;title&gt;#{pageTitle pc}
        ^{pageHead pc}
    &lt;body&gt;
        $maybe msg &lt;- mmsg
            &lt;p&gt;Your message was: #{msg}
        ^{pageBody pc}
|]

instance RenderMessage Messages FormMessage where
    renderMessage _ _ = defaultFormMessage

getRootR :: Handler RepHtml
getRootR = defaultLayout [whamlet|
&lt;form method=post action=@{SetMessageR}&gt;
    My message is: #
    &lt;input type=text name=message&gt;
    &lt;input type=submit&gt;
|]

postSetMessageR :: Handler ()
postSetMessageR = do
    msg &lt;- runInputPost $ ireq textField "message"
    setMessage $ toHtml msg
    redirect RootR

main :: IO ()
main = warpDebug 3000 Messages</code></pre>
        <figure id="x-4"><figcaption>Initial page load, no message</figcaption>
            
            <img src="../../images/messages-1.png" id="image_oka_gxb_yd" placement="inline"/>
        </figure>
        <figure id="x-6"><figcaption>New message entered in text box</figcaption>
            
            <img src="../../images/messages-2.png" id="image_bqu_hxb_yd" placement="inline"/>
        </figure>
        <figure id="x-8"><figcaption>After form submit, message appears at top of page</figcaption>
            
            <img src="../../images/messages-3.png" id="image_qhn_jxb_yd" placement="inline"/>
        </figure>
        <figure id="x-10"><figcaption>After refresh, the message is cleared</figcaption>
            
            <img src="../../images/messages-4.png" id="image_sjp_kxb_yd" placement="inline"/>
        </figure><h2>Ultimate Destination</h2><p id="u1" class="hascomments">Not to be confused with a horror film, this concept is used internally in yesod-auth. Suppose a user requests a page that requires
            authentication. If the user is not yet logged in, you need to send him/her to the login
            page. A well-designed web app will then send them back to the first page they
                requested. That's what we call the ultimate destination.</p><p id="u2" class="hascomments">redirectUltDest sends the user to the ultimate destination set
            in his/her session, clearing that value from the session. It takes a default destination
            as well, in case there is no destination set. For setting the session, there are three
                options:<ul id="ul_pms_cyz_ze">
                <li id="x-3">setUltDest sets the destination to the given URL</li>
                <li id="x-4">setUltDestCurrent sets the destination to the currently
                    requested URL.</li>
                <li id="x-5">setUltDestReferer sets the destination based on the
                        Referer header (the page that led the user to the current
                    page).</li>
            </ul></p>
        <p class="hascomments">Let's look at a small sample app. It will allow the user to set his/her name in the
            session, and then tell the user his/her name from another route. If the name hasn't been
            set yet, the user will be redirected to the set name page, with an ultimate destination
            set to come back to the current page.</p>
        <pre><code>{-# LANGUAGE OverloadedStrings, TypeFamilies, TemplateHaskell,
             QuasiQuotes, MultiParamTypeClasses #-}
import Yesod

data UltDest = UltDest

mkYesod "UltDest" [parseRoutes|
/ RootR GET
/setname SetNameR GET POST
/sayhello SayHelloR GET
|]

instance Yesod UltDest

instance RenderMessage UltDest FormMessage where
    renderMessage _ _ = defaultFormMessage

getRootR = defaultLayout [whamlet|
&lt;p&gt;
    &lt;a href=@{SetNameR}&gt;Set your name
&lt;p&gt;
    &lt;a href=@{SayHelloR}&gt;Say hello
|]

-- Display the set name form
getSetNameR = defaultLayout [whamlet|
&lt;form method=post&gt;
    My name is #
    &lt;input type=text name=name&gt;
    . #
    &lt;input type=submit value="Set name"&gt;
|]

-- Retreive the submitted name from the user
postSetNameR :: Handler ()
postSetNameR = do
    -- Get the submitted name and set it in the session
    name &lt;- runInputPost $ ireq textField "name"
    setSession "name" name

    -- After we get a name, redirect to the ultimate destination.
    -- If no destination is set, default to the homepage
    redirectUltDest RootR

getSayHelloR = do
    -- Lookup the name value set in the session
    mname &lt;- lookupSession "name"
    case mname of
        Nothing -&gt; do
            -- No name in the session, set the current page as
            -- the ultimate destination and redirect to the
            -- SetName page
            setUltDestCurrent
            setMessage "Please tell me your name"
            redirect SetNameR
        Just name -&gt; defaultLayout [whamlet|
&lt;p&gt;Welcome #{name}
|]

main :: IO ()
main = warpDebug 3000 UltDest</code></pre><h2>Summary</h2>
        <p id="x-3" class="hascomments">Sessions are the number one way we bypass the statelessness imposed by HTTP. We
            shouldn't consider this an escape hatch to perform whatever actions we want:
            statelessness in web applications is a virtue, and we should respect it whenever
            possible. However, there are specific cases where it is vital to retain some state.</p><p id="s1" class="hascomments">The session API in Yesod is very simple. It provides a key-value store, and a few
            convenience functions built on top for common use cases. If used properly, with small
            payloads, sessions should be an unobtrusive part of your web development.</p></html>