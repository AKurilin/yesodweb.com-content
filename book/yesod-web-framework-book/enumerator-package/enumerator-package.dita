<?xml version="1.0" encoding="UTF-8"?>
<html><h1>Enumerator Package</h1>
        <aside id="x-3" class="note">Due to the fact that Yesod has moved on to <a href="http://www.yesodweb.com/book/conduit" id="x-4">conduits</a>, this chapter will no longer be included in the actual Yesod book.
            It is kept on the site as a reference for those who are still using it.</aside><p id="intro1" class="hascomments">One of the upcoming patterns in Haskell is <dfn id="x1">enumerators</dfn>. They are designed to solve the problems of producing, modifying and consuming streams of data. Enumerators are considered a bit intimidating, possibly because:</p><ul id="x2"><li id="x3">There are multiple implementations, all with slightly different approaches.</li><li id="x4">Some of the implementations (in my opinion) use incredibly confusing naming.</li><li id="x5">The tutorials that get written usually don't directly target an existing implementation, and work more on building up intuition than giving instructions on how to use the library.</li></ul><p id="intro2" class="hascomments">The Yesod framework uses enumerators behind the scenes in a number of places: in WAI/Warp for dealing with request and response bodies, in Persistent for receiving the results from a database query, and in various side packages like http-enumerator and xml-enumerator. We always use the enumerator package. That package is the topic of this chapter.</p><p id="intro3" class="hascomments">This chapter is composed of three main sections, each dealing with one of the main concepts:</p><ul id="x9"><li id="x10">We start with <dfn id="x11">iteratees</dfn>, which are consumers. They are fed data and do something with it.</li><li id="x13">Next we cover <dfn id="x14">enumerators</dfn> which are producers. They feed data to an iteratee.</li><li id="x16">Finally, we address <dfn id="x17">enumeratees</dfn>. These are pipes which are fed data from an enumerator and in turn feed data to an iteratee.</li></ul><aside id="x19" class="note"><p id="intro4" class="hascomments">You may be wondering why we bother with this, when most of these problems can be addressed by lazy I/O. The basic problem with lazy I/O is its non-determinism. For more information, it's best to go to the original source on this topic: <a href="http://okmij.org/ftp/Streams.html#iteratee" id="x20">Oleg</a>.</p></aside><h2>Iteratees</h2>
        <p id="x-3" class="hascomments">An Iteratee is a data consumer. It is the core type of the enumerator package, on which
            the other types are based.</p>
    <h3>Intuition</h3><p id="intuition1" class="hascomments">Let's say we want to write a function that sums the numbers in a list. Forgetting uninteresting details like space leaks, a perfectly good implementation could be:</p><pre id="x1">sum1 :: [Int] -&gt; Int
sum1 [] = 0
sum1 (x:xs) = x + sum1 xs</pre><p id="intuition2" class="hascomments">But let's say that we don't have a list of numbers. Instead, the user is typing numbers on the command line, and hitting "q" when done. In other words, we have a function like:</p><pre id="x2">getNumber :: IO (Maybe Int)
getNumber = do
    x &lt;- readLine
    if x == "q"
        then return Nothing
        else return $ Just $ read x</pre><p id="intuition3" class="hascomments">We could write our new sum function as:</p><pre id="x3">sum2 :: IO Int
sum2 = do
    maybeNum &lt;- getNumber
    case maybeNum of
        Nothing -&gt; return 0
        Just num -&gt; do
            rest &lt;- sum2
            return $ num + rest</pre><p id="intuition4" class="hascomments">It's fairly annoying to have to write two completely separate sum functions just because our data source changed. Ideally, we would like to generalize things a bit. Let's start by noticing a similarity between these two functions: they both only yield a value when they are informed that there are no more numbers. In the case of sum1, we check for an empty list; in sum2, we check for Nothing.</p><h3>Stream Datatype</h3><p id="stream1" class="hascomments">The first datatype defined in the enumerator package is:</p><pre id="x1">data Stream a = Chunks [a] | EOF</pre><p id="stream2" class="hascomments">The EOF constructor indicates that no more data is available. The Chunks constructor simply allows us to put multiple pieces of data together for efficiency. We could now rewrite sum2 to use this Stream datatype:</p><pre id="x2">getNumber2 :: IO (Stream Int)
getNumber2 = do
    maybeNum &lt;- getNumber -- using the original getNumber function
    case maybeNum of
        Nothing -&gt; return EOF
        Just num -&gt; return $ Chunks [num]

sum3 :: IO Int
sum3 = do
    stream &lt;- getNumber2
    case stream of
        EOF -&gt; return 0
        Chunks nums -&gt; do
            let nums' = sum nums
            rest &lt;- sum3
            return $ nums' + rest</pre><p id="stream3" class="hascomments">Not that it's much better than sum2, but at least it shows how to use the Stream datatype. The problem here is that we still refer explicitly to the getNumber2 function, hard-coding the data source.</p><p id="stream4" class="hascomments">One possible solution is to make the data source an argument to the sum function, ie:</p><pre id="x3">sum4 :: IO (Stream Int) -&gt; IO Int
sum4 getNum = do
    stream &lt;- getNum
    case stream of
        EOF -&gt; return 0
        Chunks nums -&gt; do
            let nums' = sum nums
            rest &lt;- sum4 getNum
            return $ nums' + rest</pre><p id="stream5" class="hascomments">That's all well and good, but let's pretend we want to have two datasources to sum over: values the user enters on the command line, and some numbers we read over an HTTP connection, perhaps. The problem here is one of control: sum4 is running the show here by calling getNum. This is a pull data model. Enumerators have an inversion of control/push model, putting the enumerator in charge. This allows cool things like feeding in multiple data sources, and also makes it easier to write enumerators that properly deal with resource allocation.</p><h3>The Step datatype</h3><p id="step1" class="hascomments">So we need a new datatype that will represent the state of our summing operation. We're going to allow our operations to be in one of three states:</p><ul id="x1"><li id="x2">Waiting for more data.</li><li id="x3">Already calculated a result.</li><li id="x4">For convenience, we also have an error state. This isn't strictly necessary (it could be modeled by choosing an EitherT kind of monad, for example), but it's simpler.</li></ul><p id="step2" class="hascomments">As you could guess, these states will correspond to three constructors for the Step datatype. The error state is modeled by Error SomeException, building on top of Haskell's extensible exception system. The already calculated constructor is:</p><pre id="x6">Yield b (Stream a)</pre><p id="step3" class="hascomments">Here, a is the input to our iteratee and b is the output. This constructor allows us to simultaneously produce a result and save any "leftover" input for another iteratee that may run after us. (This won't be the case with the sum function, which always consumes all its input, but we'll see some other examples that do not consume all output.)</p><p id="step4" class="hascomments">Now the question is how to represent the state of an iteratee that's waiting for more data. You might at first want to declare some datatype to represent the internal state and pass that around somehow. That's not how it works: instead, we simply use a function (very Haskell of us, right?):</p><pre id="x9">Continue (Stream a -&gt; Iteratee a m b)</pre><p id="step5" class="hascomments">Eureka! We've finally seen the Iteratee datatype! Actually, Iteratee is a very boring datatype that is only present to allow us to declare cool instances (eg, Monad) for our functions. Iteratee is defined as:</p><pre id="x10">newtype Iteratee a m b = Iteratee (m (Step a m b))</pre><p id="step5a" class="hascomments">And the complete Step datatype is:</p><pre id="x11">data Step a m b = Error SomeException 
                | Yield b (Stream a) 
                | Continue (Stream a -&gt; Iteratee a m b)</pre><p id="step6" class="hascomments">This is important: Iteratee is just a newtype wrapper around a Step inside a monad. Just keep that in mind as you look at definitions in the enumerator package. So knowing this, we can think of the Continue constructor as:</p><pre id="x13">Continue (Stream a -&gt; m (Step a m b))</pre><p id="step7" class="hascomments">That's much easier to approach: that function takes some input data and returns a new state of the iteratee. Let's see what our sum function would look like using this Step datatype:</p><pre id="x14">sum5 :: Monad m =&gt; Step Int m Int -- Int input, any monad, Int output
sum5 =
    Continue $ go 0 -- a common pattern, you always start with a Continue
  where
    go :: Monad m =&gt; Int -&gt; Stream Int -&gt; Iteratee Int m Int
    -- Add the new input to the running sum and create a new Continue
    go runningSum (Chunks nums) = do
        let runningSum' = runningSum + sum nums
        -- This next line is *ugly*, good thing there are some helper
        -- functions to clean it up. More on that below.
        Iteratee $ return $ Continue $ go runningSum'
    -- Produce the final result
    go runningSum EOF = Iteratee $ return $ Yield runningSum EOF</pre>
<aside id="x15" class="note">In order to run this code, you can use run_ $ enumList 8 [1..10] sum5. But this gets into some of the Enumerator black magic we won't discuss till later.</aside>
<p id="step8" class="hascomments">The first real line (Continue $ go 0) initializes our iteratee to its starting state. Just like every other sum function, we need to explicitly state that we are starting from 0 somewhere. The real workhorse is the go function. Notice how we are really passing the state of the iteratee around as the first argument to go: this is also a very common pattern in iteratees.</p><p id="step9" class="hascomments">We need to handle two different cases: when handed an EOF, the go function must Yield a value. (Well, it could also produce an Error value, but it definitely cannot Continue.) In that case, we simply yield the running sum and say there was no data left over. When we receive some input data via Chunks, we simply add it to the running sum and create a new Continue based on the same go function.</p><p id="step10" class="hascomments">Now let's work on making that function a little bit prettier by using some built-in helper functions. The pattern Iteratee . return is common enough to warrant a helper function, namely:</p><pre id="x21">returnI :: Monad m =&gt; Step a m b -&gt; Iteratee a m b
returnI = Iteratee . return</pre><p id="step11" class="hascomments">So for example,</p><pre id="x22">go runningSum EOF = Iteratee $ return $ Yield runningSum EOF</pre><p id="step12" class="hascomments">becomes</p><pre id="x23">go runningSum EOF = returnI $ Yield runningSum EOF</pre><p id="step13" class="hascomments">But even that is common enough to warrant a helper function:</p><pre id="x25">yield :: Monad m =&gt; b -&gt; Stream a -&gt; Iteratee a m b
yield x chunk = returnI $ Yield x chunk</pre><p id="step14" class="hascomments">so our line becomes</p><pre id="x26">go runningSum EOF = yield runningSum EOF</pre><p id="step15" class="hascomments">Similarly,</p><pre id="x27">Iteratee $ return $ Continue $ go runningSum'</pre><p id="step16" class="hascomments">becomes</p><pre id="x28">continue $ go runningSum'</pre><h3>Monad instance for Iteratee</h3><p id="mi1" class="hascomments">This is all very nice: we now have an iteratee that can be fed numbers from any monad and sum them. It can even take input from different sources and sum them together. (By the way, I haven't actually shown you how to feed those numbers in: that is in part 2 about enumerators.) But let's be honest: sum5 is an ugly function. Isn't there something easier?</p><p id="mi2" class="hascomments">In fact, there is. Remember how I said Iteratee really just existed to facilitate typeclass instances? This includes a monad instance. Feel free to look at the code to see how that instance is defined, but here we'll just look at how to use it:</p><pre id="x1">sum6 :: Monad m =&gt; Iteratee Int m Int
sum6 = do
    maybeNum &lt;- head -- not head from Prelude!
    case maybeNum of
        Nothing -&gt; return 0
        Just i -&gt; do
            rest &lt;- sum6
            return $ i + rest</pre><p id="mi3" class="hascomments">That head function is not from Prelude, it's from the Data.Enumerator module. Its type signature is:</p><pre id="x2">head :: Monad m =&gt; Iteratee a m (Maybe a)</pre><p id="mi4" class="hascomments">which basically means give me the next piece of input if it's there. We'll look at this function in more depth in a bit.</p><p id="mi5" class="hascomments">Go compare the code for sum6 with sum2: they are amazingly similar. You can often build up more complicated iteratees by using some simple iteratees and the Monad instance of Iteratee.</p><h3>Interleaved I/O</h3><p id="io1" class="hascomments">Alright, let's look at a totally different problem. We want to be fed some strings and print them to the screen one line at a time. One approach would be to use lazy I/O:</p><pre id="x1">lazyIO :: IO ()
lazyIO = do
    s &lt;- lines `fmap` getContents
    mapM_ putStrLn s</pre><p id="io2" class="hascomments">But this has two drawbacks: </p><ul id="x2"><li id="x3">It's tied down to a single input source, stdin. This could be worked around with an argument giving a datasource.</li><li id="x4">But let's say the data source is some scarce resource (think: file handles on a very busy web server). We have no guarantees with lazy I/O of when those file handles will be released.</li></ul><p id="io3" class="hascomments">Let's look at how to write this in our new high-level monadic iteratee approach:</p><pre id="x5">interleaved :: MonadIO m =&gt; Iteratee String m ()
interleaved = do
    maybeLine &lt;- head
    case maybeLine of
        Nothing -&gt; return ()
        Just line -&gt; do
            liftIO $ putStrLn line
            interleaved</pre><p id="io4" class="hascomments">The liftIO function comes from the transformers package, and simply promotes an action in the IO monad to any arbitrary MonadIO action. Notice how we don't really track any state with this iteratee: we don't care about its result, only its side effects.</p><h3>Implementing head</h3><p id="ihead1" class="hascomments">As a last example, let's actually implement the head function.</p><pre id="x1">head' :: Monad m =&gt; Iteratee a m (Maybe a)
head' =
    continue go
  where
    go (Chunks []) = continue go
    go (Chunks (x:xs)) = yield (Just x) (Chunks xs)
    go EOF = yield Nothing EOF</pre><p id="ihead2" class="hascomments">Like our sum6 function, this also wraps an inner "go" function with a continue. However, we now have three clauses for our go function. The first handles the case of Chunks []. To quote the enumerator docs:</p><p id="ihead3" class="hascomments">(Chunks []) is used to indicate that a stream is still active, but currently has no available data. Iteratees should ignore empty chunks.</p><p id="ihead4" class="hascomments">The second clause handles the case where we are given some data. In this case, we yield the first element in the list, and return the rest as leftover data. The third clause handles the end of input by returning Nothing.</p><h3>Exercises</h3><ol id="x1"><li id="x2">Write an enumeratee which takes hex chars (eg, "DEADBEEF") to Word8s. Its type signature should be Enumeratee Char Word8 m b.</li><li id="x4">Write the opposite enumeratee, eg Enumeratee Word8 Char m b.</li><li id="x6">Create a quickcheck property that ensures that these two functions work correctly.</li></ol><h3>Summary</h3><ul id="x1"><li id="x2">Enumeratees are the pipes connecting enumerators to iteratees.</li><li id="x3">The strange type signature of an Enumeratee hides a lot of possible power. Especially notice how similar their type signatures are to Enumerators.</li><li id="x4">You can merge an Enumeratee into an Iteratee with joinI $ enumeratee $$ iteratee.</li><li id="x6">Don't forget that you can use the Monad instance of Iteratee when creating your own enumeratees.</li><li id="x7">You can always compose multiple enumeratees together, such as in http-enumerator.</li></ul><h2>Enumerators</h2>
        <p id="x-3" class="hascomments">Whereas an Iteratee is a consumer, an Enumerator is a producer. At a high level, an
            Enumerator is in fact an Iteratee transformer, moving an Iteratee from one state
            to a new one. Let's look at how this works.</p>
    <h3>Extracting a value</h3><p id="extract1" class="hascomments">So far, we've written a few iteratees, but we still don't know how to extract values from them. To start, let's remember that Iteratee is just a newtype wrapper around Step:</p><pre id="x3">newtype Iteratee a m b = Iteratee { runIteratee :: m (Step a m b) }</pre><p id="extract2" class="hascomments">First we need to unwrap the Iteratee and deal with the Step value inside. Remember also that Step has three constructors: Continue, Yield and Error. We'll handle the Error constructor by returning our result in an Either. Yield already provides the data we're looking for.</p><p id="extract3" class="hascomments">The tricky case is Continue: here, we have an iteratee that is still expecting more data. This is where the EOF constructor comes in handy: it's our little way to tell the iteratee to finish what it's doing and get on with things. If you remember from above, I said a well-behaving iteratee will never return a Continue after receiving an EOF; now we'll see why:</p><pre id="x4">extract :: Monad m =&gt; Iteratee a m b -&gt; m (Either SomeException b)
extract (Iteratee mstep) = do
    step &lt;- mstep
    case step of
        Continue k -&gt; do
            let Iteratee mstep' = k EOF
            step' &lt;- mstep'
            case step' of
                Continue _ -&gt; error "Misbehaving iteratee"
                Yield b _ -&gt; return $ Right b
                Error e -&gt; return $ Left e
        Yield b _ -&gt; return $ Right b
        Error e -&gt; return $ Left e</pre><p id="extract4" class="hascomments">Fortunately, you don't need to redefine this yourself: enumerator includes both a
                <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:run" id="x5">run</a> and <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:run_" id="x6">run_</a> function. Let's go ahead and use it on
            our sum6 function:</p><pre id="x7">run_ = return
sum6 = 5 :: Int
-- START
main = run_ sum6 &gt;&gt;= print</pre><p id="extract5" class="hascomments">If you run this, the result will be 0. This emphasizes an important point: an iteratee is not just how to process incoming data, it is the state of the processing. In this case, we haven't done anything to change the initial state of sum6, so we still have the initial value of 0.</p><p id="extract6" class="hascomments">To give an analogy: think of an iteratee as a machine. When you feed it data, you modify the internal state but you can't see any of those changes on the outside. When you are done feeding the data, you press a button and it spits out the result. If you don't feed in any data, your result is the initial state.</p><h3>Adding data</h3><p id="addingdata1" class="hascomments">Let's say that we actually want to sum some numbers. For example, the numbers 1 to 10. We need some way to feed that into our sum6 iteratee. In order to approach this, we'll once again need to unwrap our Iteratee and deal with the Step value directly.</p><p id="addingdata2" class="hascomments">In our case, we know with certainty that the Step constructor we used is Continue, so it's safe to write our function as:</p><pre id="x1">sum7 :: Monad m =&gt; Iteratee Int m Int
sum7 = Iteratee $ do
    Continue k &lt;- runIteratee sum6
    runIteratee $ k $ Chunks [1..10]</pre><p id="addingdata3" class="hascomments">But in general, we won't know what constructor will be lying in wait for us. We need to properly deal with Continue, Yield and Error. We've seen what to do with Continue: feed it the data. With Yield and Error, the right action in general is to do nothing, since we've already arrived at our final result (either a successful Yield or an Error). So the "proper" way to write the above function is:</p><pre id="x3">sum8 :: Monad m =&gt; Iteratee Int m Int
sum8 = Iteratee $ do
    step &lt;- runIteratee sum6
    case step of
        Continue k -&gt; runIteratee $ k $ Chunks [1..10]
        _ -&gt; return step</pre><h3>Enumerator type synonym</h3><p id="ets1" class="hascomments">What we've done with sum7 and sum8 is perform a transformation on the Iteratee. But we've done this in a very limited way: we've hard-coded in the original Iteratee function (sum6). We could just make this an argument to the function:</p><pre id="x1">sum9 :: Monad m =&gt; Iteratee Int m Int -&gt; Iteratee Int m Int
sum9 orig = Iteratee $ do
    step &lt;- runIteratee orig
    case step of
        Continue k -&gt; runIteratee $ k $ Chunks [1..10]
        _ -&gt; return step</pre><p id="ets2" class="hascomments">But since we always just want to unwrap the Iteratee value anyway, it turns out that it's more natural to make the argument of type Step, ie:</p><pre id="x2">sum10 :: Monad m =&gt; Step Int m Int -&gt; Iteratee Int m Int
sum10 (Continue k) = k $ Chunks [1..10]
sum10 step = returnI step</pre><p id="ets3" class="hascomments">This type signature (take a Step, return an Iteratee) turns out to be very common:</p><pre id="x3">type Enumerator a m b = Step a m b -&gt; Iteratee a m b</pre><p id="ets4" class="hascomments">Meaning sum10's type signature could also be expressed as:</p><pre id="x4">sum10 :: Monad m =&gt; Enumerator Int m Int</pre><p id="ets5" class="hascomments">Of course, we need some helper function to connect an Enumerator and an Iteratee:</p><pre id="x5">applyEnum :: Monad m =&gt; Enumerator a m b -&gt; Iteratee a m b -&gt; Iteratee a m b
applyEnum enum iter = Iteratee $ do
    step &lt;- runIteratee iter
    runIteratee $ enum step</pre><p id="ets6" class="hascomments">Let me repeat the intuition here: the Enumerator is transforming the Iteratee from its initial state to a new state by feeding it more data. In order to use this function, we could write:</p><pre id="x6">run_ (applyEnum sum10 sum6) &gt;&gt;= print</pre><p id="ets7" class="hascomments">This results in 55, exactly as we'd expect. But now we can see one of the benefits of enumerators: we can use multiple data sources. Let's say we have another enumerator:</p><pre id="x7">sum11 :: Monad m =&gt; Enumerator Int m Int
sum11 (Continue k) = k $ Chunks [11..20]
sum11 step = returnI step</pre><p id="ets8" class="hascomments">Then we could simply apply both enumerators:</p><pre id="x8">run_ (applyEnum sum11 $ applyEnum sum10 sum6) &gt;&gt;= print</pre><p id="ets9" class="hascomments">And we would get the result 210. (Yes, (1 + 20) * 10 = 210.) But don't worry, you don't
            need to write this applyEnum function yourself: enumerator provides a <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:-36--36-" id="x9">$$</a> operator which does the same thing. Its
            type signature is a bit scarier, since it's a generalization of applyEnum, but it works
            the same, and even makes code more readable:</p><pre id="x10">run_ (sum11 $$ sum10 $$ sum6) &gt;&gt;= print</pre><p id="ets10" class="hascomments">$$ is a synonym for <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:-61--61--60--60-" id="x12">==&lt;&lt;</a>, which is simply flip <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:-62--62--61--61-" id="x13">&gt;&gt;==</a>. I find $$ the most readable, but YMMV (YMMV).</p><h3>Some built-in enumerators</h3><p id="builtin1" class="hascomments">Of course, writing a whole function just to pass some numbers to our sum function seems a bit tedious. We could easily make the list an argument to the function:</p><pre id="x1">sum12 :: Monad m =&gt; [Int] -&gt; Enumerator Int m Int
sum12 nums (Continue k) = k $ Chunks nums
sum12 _ step = returnI step</pre><p id="builtin2" class="hascomments">But now there's not even anything Int-specific in our function. We could easily generalize this to:</p><pre id="x2">genericSum12 :: Monad m =&gt; [a] -&gt; Enumerator a m b
genericSum12 nums (Continue k) = k $ Chunks nums
genericSum12 _ step = returnI step</pre><p id="builtin3" class="hascomments">And in fact, enumerator comes built in with the <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:enumList" id="x3">enumList</a> function which does
                        this. enumList also takes an Integer argument to indicate the maximum number
                        of elements to stick in a chunk. For example, we could write:</p><pre id="x4">run_ (enumList 5 [1..30] $$ sum6) &gt;&gt;= print</pre><p id="builtin4" class="hascomments">(That produces 465 if you're counting.) The first argument to enumList should never affect the result, though it may have some performance impact.</p><p id="builtin5" class="hascomments">Data.Enumerator includes two other enumerators: <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:enumEOF" id="x5">enumEOF</a> simply passes an EOF
                        to the iteratee. <a href="http://hackage.haskell.org/packages/archive/enumerator/0.4.0.2/doc/html/Data-Enumerator.html#v:concatEnums" id="x6">concatEnums</a> is slightly more
                        interesting; it combines multiple enumerators together. For example:</p><pre id="x7">run_ (concatEnums
        [ enumList 1 [1..10]
        , enumList 1 [11..20]
        , enumList 1 [21..30]
        ] $$ sum6) &gt;&gt;= print</pre><p id="builtin6" class="hascomments">This also produces 465.</p><h3>Some non-pure input</h3><p id="np1" class="hascomments">Enumerators are much more interesting when they aren't simply dealing with pure values. In the first part of this tutorial, we gave the example of the user entering numbers on the command line:</p><pre id="x1">getNumber :: IO (Maybe Int)
getNumber = do
    x &lt;- getLine
    if x == "q"
        then return Nothing
        else return $ Just $ read x

sum2 :: IO Int
sum2 = do
    maybeNum &lt;- getNumber
    case maybeNum of
        Nothing -&gt; return 0
        Just num -&gt; do
            rest &lt;- sum2
            return $ num + rest</pre><p id="np2" class="hascomments">We referred to this as the pull-model: sum2 pulled each value from getNumber. Let's see if we can rewrite getNumber to be a pusher instead of a pullee.</p><pre id="x2">getNumberEnum :: MonadIO m =&gt; Enumerator Int m b
getNumberEnum (Continue k) = do
    x &lt;- liftIO getLine
    if x == "q"
        then continue k
        else k (Chunks [read x]) &gt;&gt;== getNumberEnum
getNumberEnum step = returnI step</pre><p id="np3" class="hascomments">First, notice that we check which constructor was passed, and only perform any actions if it was Continue. If it was Continue, we get the line of input from the user. If the line is "q" (our indication to stop feeding in values), we do nothing. You might have thought that we should pass an EOF. But if we did that, we'd be preventing other data from being sent to this iteratee. Instead, we simply return the original Step value.</p><p id="np4" class="hascomments">If the line was not "q", we convert it to an Int via read, create a Stream value with the Chunks datatype, and pass it to k. (If we wanted to do things properly, we'd check if x is really an Int and use the Error constructor; I leave that as an exercise to the reader.) At this point, let's look at type signatures:</p><pre id="x4">k (Chunks [read x]) :: Iteratee Int m b</pre><p id="np5" class="hascomments">If we simply left off the rest of the line, our program would typecheck. However, it would only ever read one value from the command line; the &gt;&gt;== getNumberEnum causes our enumerator to loop.</p><p id="np6" class="hascomments">One last thing to note about our function: notice the b in our type signature.</p><pre id="x6">getNumberEnum :: MonadIO m =&gt; Enumerator Int m b</pre><p id="np7" class="hascomments">This is saying that our Enumerator can feed Ints to any Iteratee accepting Ints, and it doesn't matter what the final output type will be. This is in general the way enumerators work. This allows us to create drastically different iteratees that work with the same enumerators:</p><pre id="x9">intsToStrings :: (Show a, Monad m) =&gt; Iteratee a m String
intsToStrings = (unlines . map show) `fmap` consume</pre><p id="np8" class="hascomments">And then both of these lines work:</p><pre id="x10">run_ (getNumberEnum $$ sum6) &gt;&gt;= print
run_ (getNumberEnum $$ intsToStrings) &gt;&gt;= print</pre><h3>Exercises</h3><ol id="x1"><li id="x2">Write an enumeratee which takes hex chars (eg, "DEADBEEF") to Word8s. Its type signature should be Enumeratee Char Word8 m b.</li><li id="x4">Write the opposite enumeratee, eg Enumeratee Word8 Char m b.</li><li id="x6">Create a quickcheck property that ensures that these two functions work correctly.</li></ol><h3>Summary</h3><ul id="x1"><li id="x2">Enumeratees are the pipes connecting enumerators to iteratees.</li><li id="x3">The strange type signature of an Enumeratee hides a lot of possible power. Especially notice how similar their type signatures are to Enumerators.</li><li id="x4">You can merge an Enumeratee into an Iteratee with joinI $ enumeratee $$ iteratee.</li><li id="x6">Don't forget that you can use the Monad instance of Iteratee when creating your own enumeratees.</li><li id="x7">You can always compose multiple enumeratees together, such as in http-enumerator.</li></ul><h2>Enumeratees</h2>
        <p id="x-3" class="hascomments">The final piece in the puzzle is the Enumeratee. This is a combination of an Iteratee (it
            consumes some data) and an Enumerator (is passes it other data on). It can be thought of
            as a stream transformer, or as a pipe connecting an Enumerator to an
            Iteratee.</p>
    <h3>Generalizing getNumberEnum</h3><p id="gen1" class="hascomments">Earlier, we created a getNumberEnum function with a type signature:</p><pre id="x1">getNumberEnum :: MonadIO m =&gt; Enumerator Int m b</pre><p id="gen2" class="hascomments">If you don't remember, this means getNumberEnum produces a stream of Ints. In particular, our getNumberEnum function read lines from stdin, converted them to ints and fed them into an iteratee. It stopped reading lines when it saw a "q".</p><p id="gen3" class="hascomments">But this functionality seems like it could be useful outside the realm of Ints. We may like to deal with the original Strings, for example, or Bools, or a bunch of other things. We could easily define a more generalized function which simply doesn't do the String to Int conversion:</p><pre id="x3">lineEnum :: MonadIO m =&gt; Enumerator String m b
lineEnum (Continue k) = do
    x &lt;- liftIO getLine
    if x == "q"
        then continue k
        else k (Chunks [x]) &gt;&gt;== lineEnum
lineEnum step = returnI step</pre><p id="gen4" class="hascomments">Cool, let's plug this into our sumIter function (I've renamed the sum6 function from the previous two parts):</p><pre id="x4">lineEnum $$ sumIter</pre><p id="gen5" class="hascomments">Actually, that doesn't type check: lineEnum produces Strings, and sumIter takes Ints. We need to modify one of them somehow.</p><pre id="x7">sumIterString :: Monad m =&gt; Iteratee String m Int
sumIterString = Iteratee $ do
    innerStep &lt;- runIteratee sumIter
    return $ go innerStep
  where
    go :: Monad m =&gt; Step Int m Int -&gt; Step String m Int
    go (Yield res _) = Yield res EOF
    go (Error err) = Error err
    go (Continue k) = Continue $ \strings -&gt; Iteratee $ do
        let ints = fmap read strings :: Stream Int
        step &lt;- runIteratee $ k ints
        return $ go step</pre><p id="gen6" class="hascomments">What we've done here is wrap around the original iteratee. As usual, we first need to unwrap the Iteratee constructor and the monad to get at the heart of the Step value. Once we have that innerStep value, we pass it to the go function, which simply transforms that values in the Stream value from Strings to Ints.</p><h3>Even more general</h3><p id="moregen1" class="hascomments">Of course, it would be nice if we could apply this transformation to *any* iteratee. To start with, let's just pass the inner iteratee and the mapping function as parameters.</p><pre id="x1">mapIter :: Monad m =&gt; (aOut -&gt; aIn) -&gt; Iteratee aIn m b -&gt; Iteratee aOut m b
mapIter f innerIter = Iteratee $ do
    innerStep &lt;- runIteratee innerIter
    return $ go innerStep
  where
    go (Yield res _) = Yield res EOF
    go (Error err) = Error err
    go (Continue k) = Continue $ \strings -&gt; Iteratee $ do
        let ints = fmap f strings
        step &lt;- runIteratee $ k ints
        return $ go step</pre><p id="moregen2" class="hascomments">We could call this like:</p><pre id="x2">run_ (lineEnum $$ mapIter read sumIter) &gt;&gt;= print</pre><p id="moregen3" class="hascomments">Nothing much to see here, it's basically identical to the previous version. What's funny is that enumerator comes built in with a map function to do just this, but it has a significantly different type signature:</p><pre id="x4">map :: Monad m =&gt; (ao -&gt; ai) -&gt; Enumeratee ao ai m b</pre><p id="moregen4" class="hascomments">since:</p><pre id="x5">type Enumeratee aOut aIn m b = Step aIn m b -&gt; Iteratee aOut m (Step aIn m b)</pre><p id="moregen5" class="hascomments">that's equivalent to:</p><pre id="x6">map :: Monad m =&gt; (aOut -&gt; aIn) -&gt; Step aIn m b -&gt; Iteratee aOut m (Step aIn m b)</pre><p id="moregen6" class="hascomments">What's with all this extra complication in type signature? Well, it's not necessary for map itself, but it is necessary for a whole bunch of other similar functions. But let's focus on this map for a second so we don't get lost: the first argument is the same old mapping function we had before. The second argument is a Step value. This isn't really so surprising: in our mapIter, we took an Iteratee with the same parameters, and we internally just unwrapped it to a Step.</p><p id="moregen7" class="hascomments">But what's happening with that return value? Remembering the meanings for all these datatypes, it's an Iteratee which will be fed a stream of aOuts and return a Step (aka, a new iteratee, right?). This kind of makes intuitive sense: we've introduced a middle man which accepts input from one source and transforms a Step to a newer state.</p><p id="moregen8" class="hascomments">But now perhaps the trickiest part of the whole thing: how do we actually use this map function? It turns out that an Enumeratee is close enough in type signature to an Enumerator that we can just do:</p><pre id="x10">map read $$ sumIter</pre><p id="moregen9" class="hascomments">But the type signature on that turns out to be a little bit weird:</p><pre id="x12">Iteratee String m (Step Int m Int)</pre><p id="moregen10" class="hascomments">Remembering that an Iteratee is just a wrapped up Step, what we've got here is an iteratee that takes Strings and returns an Iteratee, which in turn takes Ints and produces an Int. Having this fancy result allows us to do one of our great tricks with iteratees: plug in data from multiple sources. For example, we could plug some Strings into this whole ugly thing, run it, get a new iteratee which takes Ints, feed that some Ints and get an Int result.</p><p id="moregen11" class="hascomments">(If all that went over your head, don't worry. I won't be talking about that kind of stuff any more.)</p><p id="moregen12" class="hascomments">But often times, we don't need all of that power. We just want to stick our enumeratee onto our iteratee and get a new iteratee. In our case, we want to attach our map onto the sumIter to produce a new iteratee that takes Strings and returns Ints. In order to do that, we need a function like this:</p><pre id="x17">unnest :: Monad m =&gt; Iteratee String m (Step Int m Int) -&gt; Iteratee String m Int
unnest outer = do -- using the Monad instance of Iteratee
    inner &lt;- outer -- inner :: Step Int m Int
    go inner
  where
    go (Error e) = throwError e
    go (Yield x _) = yield x EOF
    go (Continue k) = k EOF &gt;&gt;== go</pre><p id="moregen13" class="hascomments">We can then run our unholy mess with:</p><pre id="x18">run_ (lineEnum $$ unnest $ map read $$ sumIter) &gt;&gt;= print</pre><p id="moregen14" class="hascomments">And actually, the unnest function is available in Data.Enumerator, and it's called joinI. So we should really write:</p><pre id="x19">run_ (lineEnum $$ joinI $ map read $$ sumIter) &gt;&gt;= print</pre><h3>Skipping</h3><p id="skipping1" class="hascomments">Let's write a slightly more interesting enumeratee: this one skips every other input value.</p><pre id="x1">skip :: Monad m =&gt; Enumeratee a a m b
skip (Continue k) = do
    x &lt;- head
    _ &lt;- head -- the one we're skipping
    case x of
        Nothing -&gt; return $ Continue k
        Just y -&gt; do
            newStep &lt;- lift $ runIteratee $ k $ Chunks [y]
            skip newStep
skip step = return step</pre><p id="skipping2" class="hascomments">What's interesting about the approach here is how similar it looks to an Enumerator. We're doing a lot of the same things: checking if the Step value is a Continue; if it's not, then simply return it. Then we capitalize on the Iteratee Monad instance, using the head function to pop two values out of the stream. If there's no more data, we return the original Continue value: just like with an Enumerator, we don't give an EOF so that we can feed more data into the iteratee later. If there is data, we pass it off to the iteratee, get our new step value and then loop.</p><p id="skipping3" class="hascomments">And what's cool about enumeratees is we can chain these all together:</p><pre id="x2">run_ (lineEnum $$ joinI $ skip $$ joinI $ map read $$ sumIter) &gt;&gt;= print</pre><p id="skipping4" class="hascomments">Here, we read lines, skip every other input, convert the Strings to Ints and sum them.</p><h3>Real life examples: http-enumerator package</h3><p id="reallife1" class="hascomments">I started working on these tutorials as I was working on the http-enumerator package. I think the usage of enumeratees there is a great explanation of the benefits they can offer in real life. There are three different ways the response body can be broken up:</p><ul id="x2"><li id="x3">Chunked encoding. In this case, the web server gives a hex string specifying the length of the next chunk and then that chunk. At the end, it sends a 0 to indicate the end of that response.</li><li id="x4">Content length. Here, the web server sends a header before any of the body is sent specifying the total length of the body.</li><li id="x5">Nothing at all. In this case, the response body lasts until an end-of-file.</li></ul><p id="reallife2" class="hascomments">In addition, the body may or may not be GZIP compressed. We end up with the following enumeratees, each with type signature Enumeratee ByteString ByteString m b: chunkedEncoding, contentLength and ungzip. We then get to do something akin to:</p><pre id="x7">let parseBody x =
        if ("transfer-encoding", "chunked") `elem` responseHeaders
            then joinI $ chunkedEncoding $$ x
            else case mlen of
                    Just len -&gt; joinI $ contentLength len $$ x
                    Nothing -&gt; x -- no enumeratee applied at all
let decompress x =
        if ("content-encoding", "gzip") `elem` responseHeaders
            then joinI $ ungzip $$ x
            else x
run_ $ socketEnumerator $$ parseBody $ decompress $ bodyIteratee</pre><p id="reallife3" class="hascomments">We create a chain: the data from the server is fed into the parseBody function. In the case of chunked encoding, the data is processed appropriately and then headers are filtered out. If we are dealing with content length, then only the specified number of bytes are read. And in the case of neither of those, parseBody is a no-op.</p><p id="reallife4" class="hascomments">Whatever the case may be, the raw response body is then fed into decompress. If the body is GZIPed, then ungzip inflates it, otherwise decompress is a no-op. Finally, the parsed and inflated data is fed into the user-supplied bodyIteratee function. The user remains blissfully unaware of any steps the data took to get to him/her.</p><h3>Exercises</h3><ol id="x1"><li id="x2">Write an enumeratee which takes hex chars (eg, "DEADBEEF") to Word8s. Its type signature should be Enumeratee Char Word8 m b.</li><li id="x4">Write the opposite enumeratee, eg Enumeratee Word8 Char m b.</li><li id="x6">Create a quickcheck property that ensures that these two functions work correctly.</li></ol><h3>Summary</h3><ul id="x1"><li id="x2">Enumeratees are the pipes connecting enumerators to iteratees.</li><li id="x3">The strange type signature of an Enumeratee hides a lot of possible power. Especially notice how similar their type signatures are to Enumerators.</li><li id="x4">You can merge an Enumeratee into an Iteratee with joinI $ enumeratee $$ iteratee.</li><li id="x6">Don't forget that you can use the Monad instance of Iteratee when creating your own enumeratees.</li><li id="x7">You can always compose multiple enumeratees together, such as in http-enumerator.</li></ul></html>