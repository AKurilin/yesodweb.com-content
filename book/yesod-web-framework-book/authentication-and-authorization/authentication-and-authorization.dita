<?xml version="1.0" encoding="UTF-8"?>
<html><h1>Authentication and Authorization</h1>
        <p id="x-3" class="hascomments">Authentication and authorization are two very related, and yet separate, concepts. While
            the former deals with identifying a user, the latter determines what a user is allowed
            to do. Unfortunately, since both terms are often abbreviated as "auth," the concepts are
            often conflated.</p>
        <p id="x-4" class="hascomments">Yesod provides built-in support for a number of third-party authentication systems, such
            as OpenID, BrowserID and OAuth. These are systems where your application trusts some
            external system for validating a user's credentials. Additionally, there is support for
            more commonly used username/password and email/password systems. The former route
            ensures simplicity for users (no new passwords to remember) and implementors (no need to
            deal with an entire security architecture), while the latter gives the developer more
            control.</p>
        <p id="x-5" class="hascomments">On the authorization side, we are able to take advantage of REST and type-safe
            URLs to create simple, declarative systems. Additionally, since all authorization code
            is written in Haskell, you have the full flexibility of the language at your
            disposal.</p>
        <p id="x-6" class="hascomments">This chapter will cover how to set up an "auth" solution in Yesod and discuss some
            trade-offs in the different authentication options.</p><h2>Overview</h2>
  <p id="x-3" class="hascomments">The yesod-auth package provides a unified interface for a
   number of different authentication plugins. The only real requirement for these backends is that
   they identify a user based on some unique string. In OpenID, for instance, this would be the
   actual OpenID value. In BrowserID, it's the email address. For HashDB (which uses a database of
   hashed passwords), it's the username.</p>
  <p id="x-5" class="hascomments">Each authentication plugin provides its own system for logging in, whether it be via
   passing tokens with an external site or a email/password form. After a successful login, the
   plugin sets a value in the user's session to indicate his/her AuthId. This
    AuthId is usually a Persistent ID from a table used for keeping track of
   users.</p>
  <p id="x-6" class="hascomments">There are a few functions available for querying a user's AuthId,
   most commonly maybeAuthId, requireAuthId,
    maybeAuth and requireAuth. The require
   versions will redirect to a login page if the user is not logged in, while the second set of
   functions (the ones not ending in Id) give both the table ID and entity value.</p>
  <p id="x-13" class="hascomments">Since all of the storage of AuthId is built on top of sessions, all
   of the rules from there apply. In particular, the data is stored in an encrypted, HMACed client
   cookie, which automatically times out after a certain configurable period of inactivity.
   Additionally, since there is no server-side component to sessions, logging out simply deletes the
   data from the session cookie; if a user reuses an older cookie value, the session will still be
   valid.</p>
  <aside id="x-14" class="note">There are plans to add in a server-side component to sessions which would allow forced
   logout. This will almost certainly be implemented before a 1.0 release of Yesod.</aside>
  <p id="x-15" class="hascomments">On the flip side, authorization is handled by a few methods inside the
    Yesod typeclass. For every request, these methods are run to determine if
   access should be allowed, denied, or if the user needs to be authenticated. By default, these
   methods allow access for every request. Alternatively, you can implement authorization in a more
   ad-hoc way by adding calls to requireAuth and the like within individual handler
   functions, though this undermines many of the benefits of a declarative authorization system.</p>
 <h2>Authenticate Me</h2>
  <p id="x-3" class="hascomments">Let's jump right in with an example of authentication.</p>
  <pre id="x-4"><code>{-# LANGUAGE OverloadedStrings, TemplateHaskell, TypeFamilies,
             MultiParamTypeClasses, QuasiQuotes #-}
import Yesod
import Yesod.Auth
import Yesod.Auth.BrowserId
import Yesod.Auth.GoogleEmail
import Data.Text (Text)
import Network.HTTP.Conduit (Manager, newManager, def)

data MyAuthSite = MyAuthSite
    { httpManager :: Manager
    }

mkYesod "MyAuthSite" [parseRoutes|
/ RootR GET
/auth AuthR Auth getAuth
|]

instance Yesod MyAuthSite where
    -- Note: In order to log in with BrowserID, you must correctly
    -- set your hostname here.
    approot = ApprootStatic "http://localhost:3000"

instance YesodAuth MyAuthSite where
    type AuthId MyAuthSite = Text
    getAuthId = return . Just . credsIdent

    loginDest _ = RootR
    logoutDest _ = RootR

    authPlugins _ =
        [ authBrowserId
        , authGoogleEmail
        ]

    authHttpManager = httpManager

instance RenderMessage MyAuthSite FormMessage where
    renderMessage _ _ = defaultFormMessage

getRootR :: Handler RepHtml
getRootR = do
    maid &lt;- maybeAuthId
    defaultLayout [whamlet|
&lt;p&gt;Your current auth ID: #{show maid}
$maybe _ &lt;- maid
    &lt;p&gt;
        &lt;a href=@{AuthR LogoutR}&gt;Logout
$nothing
    &lt;p&gt;
        &lt;a href=@{AuthR LoginR}&gt;Go to the login page
|]

main :: IO ()
main = do
    man &lt;- newManager def
    warpDebug 3000 $ MyAuthSite man</code></pre>
  <p id="x-5" class="hascomments">We'll start with the route declarations. First we declare our standard RootR
   route, and then we set up the authentication subsite. Remember that a subsite needs four
   parameters: the path to the subsite, the route name, the subsite name, and a function to get the
   subsite value. In other words, based on the line:</p>
  <pre id="x-7"><code>/auth AuthR Auth getAuth</code></pre>
  <p id="x-8" class="hascomments">We need to have getAuth :: MyAuthSite -&gt; Auth. While we haven't written that
   function ourselves, yesod-auth provides it automatically. With other subsites
   (like static files), we provide configuration settings in the subsite value, and therefore need
   to specify the get function. In the auth subsite, we specify these settings in a separate
   typeclass, YesodAuth.</p>
  <aside id="x-12" class="note">Why not use the subsite value? There are a number of settings we would like to give for an
   auth subsite, and doing so from a record type would be inconvenient. Also, since we want to have
   an AuthId associated type, a typeclass is more natural.<p id="x-14" class="hascomments">On the flip side, why
    not use a typeclass for all subsites? It comes with a downside: you can then only have a single
    instance per site, disallowing serving different sets of static files from different routes.
    Also, the subsite value works better when we want to load data at app initialization.</p></aside>
  <p id="x-15" class="hascomments">So what exactly goes in this YesodAuth instance? There are six required
   declarations:</p>
  <ul id="ul_mgj_phf_p2">
   <li id="x-16">AuthId is an associated type. This is the value
     yesod-auth will give you when you ask if a user is logged in (via
     maybeAuthId or requireAuthId). In our case, we're simply
    using Text, to store the raw identifier- email address in our case, as we'll
    soon see.</li>
   <li id="x-17">getAuthId gets the actual AuthId from the Creds (credentials) data type. This type has three pieces of information:
    the authentication backend used (browserid or googleemail in our case), the actual identifier,
    and an associated list of arbitrary extra information. Each backend provides different extra
    information; see their docs for more information.</li>
   <li id="x-19">loginDest gives the route to redirect to after a successful
    login.</li>
   <li id="x-20">Likewise, logoutDest gives the route to redirect to after a
    logout.</li>
   <li id="x-21">authPlugins is a list of individual authentication backends to
    use. In our example, we're using BrowserID, which logs in via Mozilla's BrowserID system, and
    Google Email, which authenticates a user's email address using their Google account. The nice
    thing about these two backends is:<ul id="ul_xwj_f3f_p2">
     <li id="x-22">They require no set up, as opposed to Facebook or OAuth, which require setting up
      credentials.</li>
     <li id="x-23">They use email addresses as identifiers, which people are comfortable with, as
      opposed to OpenID, which uses a URL.</li>
    </ul></li>
   <li id="x-42">authHttpManager gets an HTTP connection manager from the foundation type.
    This allow authentication backends which use HTTP connections (i.e., almost all third-party
    login systems) to share connections, avoiding the cost of restarting a TCP connection for each
    request.</li>
  </ul>
  <p id="x-24" class="hascomments">In our RootR handler, we have some simple links to the login and
   logout pages, depending on whether or not the user is logged in. Notice how we construct these
   subsite links: first we give the subsite route name (AuthR), followed by the
   route within the subsite (LoginR and LogoutR).</p>
  <p id="x-25" class="hascomments">The figures below show what the login process looks like from a user perspective.</p>
  <figure id="x-26"><figcaption>Initial page load</figcaption>
   
   <img src="../../images/initial-screen.png" id="image_vw4_r3f_p2" placement="inline"/>
  </figure>
  <figure id="x-28"><figcaption>BrowserID login screen</figcaption>
   
   <img src="../../images/login-with-browserid.png" id="image_jp4_t3f_p2" placement="inline"/>
  </figure>
  <figure id="x-30"><figcaption>Homepage after logging in</figcaption>
   
   <img src="../../images/after-login.png" id="image_gz2_v3f_p2" placement="inline"/>
  </figure>
 <h2>Email</h2>
  <p id="x-3" class="hascomments">For many use cases, third-party authentication of email will be sufficient. Occassionally,
   you'll want users to actual create passwords on your site. The scaffolded site does not include
   this setup, because:</p>
  <ul id="ul_mmf_nsk_pe">
   <li id="x-4">In order to securely accept passwords, you need to be running over SSL. Many users are not
    serving their sites over SSL.</li>
   <li id="x-5">While the email backend properly salts and hashes passwords, a compromised database could
    still be problematic. Again, we make no assumptions that Yesod users are following secure
    deployment practices.</li>
   <li id="x-6">You need to have a working system for sending email. Many web servers these days are
                not equipped to deal with all of the spam protection measures used by mail
                    servers.<aside id="x-7" class="note">The example below will use the system's built-in sendmail executable. If you would like to avoid the
                    hassle of dealing with an email server yourself, you can use Amazon SES. There
                    is a package called mime-mail-ses which provides a
                    drop-in replacement for the sendmail code used below. This is the approach we
                    use on the Haskellers.com site.</aside></li>
  </ul>
  <p id="x-10" class="hascomments">But assuming you are able to meet these demands, and you want to have a separate password login
   specifically for your site, Yesod offers a built-in backend. It requires quite a bit of code to
   set up, since it needs to store passwords securely in the database and send a number of different
   emails to users (verify account, password retrieval, etc.).</p>
  <p id="x-11" class="hascomments">Let's have a look at a site that provides email authentication, storing passwords in a
   Persistent SQLite database.</p>
  <pre id="x-12"><code>{-# LANGUAGE OverloadedStrings, TypeFamilies, QuasiQuotes, GADTs,
             TemplateHaskell, MultiParamTypeClasses, FlexibleContexts #-}
import Yesod
import Yesod.Auth
import Yesod.Auth.Email
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Text (Text)
import Network.Mail.Mime
import qualified Data.Text.Lazy.Encoding
import Text.Shakespeare.Text (stext)
import Text.Blaze.Renderer.Utf8 (renderHtml)
import Text.Hamlet (shamlet)
import Data.Maybe (isJust)
import Control.Monad (join)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
User
    email Text
    password Text Maybe -- Password may not be set yet
    verkey Text Maybe -- Used for resetting passwords
    verified Bool
    UniqueUser email
|]

data MyEmailApp = MyEmailApp Connection

mkYesod "MyEmailApp" [parseRoutes|
/ RootR GET
/auth AuthR Auth getAuth
|]

instance Yesod MyEmailApp where
    -- Emails will include links, so be sure to include an approot so that
    -- the links are valid!
    approot = ApprootStatic "http://localhost:3000"

instance RenderMessage MyEmailApp FormMessage where
    renderMessage _ _ = defaultFormMessage

-- Set up Persistent
instance YesodPersist MyEmailApp where
    type YesodPersistBackend MyEmailApp = SqlPersist
    runDB f = do
        MyEmailApp conn &lt;- getYesod
        runSqlConn f conn

instance YesodAuth MyEmailApp where
    type AuthId MyEmailApp = UserId

    loginDest _ = RootR
    logoutDest _ = RootR
    authPlugins _ = [authEmail]

    -- Need to find the UserId for the given email address.
    getAuthId creds = runDB $ do
        x &lt;- insertBy $ User (credsIdent creds) Nothing Nothing False
        return $ Just $
            case x of
                Left (Entity userid _) -&gt; userid -- newly added user
                Right userid -&gt; userid -- existing user

    authHttpManager = error "Email doesn't need an HTTP manager"

-- Here's all of the email-specific code
instance YesodAuthEmail MyEmailApp where
    type AuthEmailId MyEmailApp = UserId

    addUnverified email verkey =
        runDB $ insert $ User email Nothing (Just verkey) False

    sendVerifyEmail email _ verurl =
        liftIO $ renderSendMail (emptyMail $ Address Nothing "noreply")
            { mailTo = [Address Nothing email]
            , mailHeaders =
                [ ("Subject", "Verify your email address")
                ]
            , mailParts = [[textPart, htmlPart]]
            }
      where
        textPart = Part
            { partType = "text/plain; charset=utf-8"
            , partEncoding = None
            , partFilename = Nothing
            , partContent = Data.Text.Lazy.Encoding.encodeUtf8 [stext|
Please confirm your email address by clicking on the link below.

\#{verurl}

Thank you
|]
            , partHeaders = []
            }
        htmlPart = Part
            { partType = "text/html; charset=utf-8"
            , partEncoding = None
            , partFilename = Nothing
            , partContent = renderHtml [shamlet|
&lt;p&gt;Please confirm your email address by clicking on the link below.
&lt;p&gt;
    &lt;a href=#{verurl}&gt;#{verurl}
&lt;p&gt;Thank you
|]
            , partHeaders = []
            }
    getVerifyKey = runDB . fmap (join . fmap userVerkey) . get
    setVerifyKey uid key = runDB $ update uid [UserVerkey =. Just key]
    verifyAccount uid = runDB $ do
        mu &lt;- get uid
        case mu of
            Nothing -&gt; return Nothing
            Just u -&gt; do
                update uid [UserVerified =. True]
                return $ Just uid
    getPassword = runDB . fmap (join . fmap userPassword) . get
    setPassword uid pass = runDB $ update uid [UserPassword =. Just pass]
    getEmailCreds email = runDB $ do
        mu &lt;- getBy $ UniqueUser email
        case mu of
            Nothing -&gt; return Nothing
            Just (Entity uid u) -&gt; return $ Just EmailCreds
                { emailCredsId = uid
                , emailCredsAuthId = Just uid
                , emailCredsStatus = isJust $ userPassword u
                , emailCredsVerkey = userVerkey u
                }
    getEmail = runDB . fmap (fmap userEmail) . get

getRootR :: Handler RepHtml
getRootR = do
    maid &lt;- maybeAuthId
    defaultLayout [whamlet|
&lt;p&gt;Your current auth ID: #{show maid}
$maybe _ &lt;- maid
    &lt;p&gt;
        &lt;a href=@{AuthR LogoutR}&gt;Logout
$nothing
    &lt;p&gt;
        &lt;a href=@{AuthR LoginR}&gt;Go to the login page
|]

main :: IO ()
main = withSqliteConn "email.db3" $ \conn -&gt; do
    runSqlConn (runMigration migrateAll) conn
    warpDebug 3000 $ MyEmailApp conn</code></pre>
 <h2>Authorization</h2>
  <p id="x-3" class="hascomments">Once you can authenticate your users, you can use their credentials to authorize requests. Authorization in Yesod is simple and declarative: most of
            the time, you just need to add the authRoute and
                isAuthorized methods to your Yesod typeclass instance. Let's see an
            example.</p>
  <pre id="x-5"><code>{-# LANGUAGE OverloadedStrings, TemplateHaskell, TypeFamilies,
             MultiParamTypeClasses, QuasiQuotes #-}
import Yesod
import Yesod.Auth
import Yesod.Auth.Dummy -- just for testing, don't use in real life!!!
import Data.Text (Text)
import Network.HTTP.Conduit (Manager, newManager, def)

data MyAuthSite = MyAuthSite
    { httpManager :: Manager
    }

mkYesod "MyAuthSite" [parseRoutes|
/ RootR GET POST
/admin AdminR GET
/auth AuthR Auth getAuth
|]

instance Yesod MyAuthSite where
    authRoute _ = Just $ AuthR LoginR

    -- route name, then a boolean indicating if it's a write request
    isAuthorized RootR True = isAdmin
    isAuthorized AdminR _ = isAdmin

    -- anyone can access other pages
    isAuthorized _ _ = return Authorized

isAdmin = do
    mu &lt;- maybeAuthId
    return $ case mu of
        Nothing -&gt; AuthenticationRequired
        Just "admin" -&gt; Authorized
        Just _ -&gt; Unauthorized "You must be an admin"

instance YesodAuth MyAuthSite where
    type AuthId MyAuthSite = Text
    getAuthId = return . Just . credsIdent

    loginDest _ = RootR
    logoutDest _ = RootR

    authPlugins _ = [authDummy]

    authHttpManager = httpManager

instance RenderMessage MyAuthSite FormMessage where
    renderMessage _ _ = defaultFormMessage

getRootR :: Handler RepHtml
getRootR = do
    maid &lt;- maybeAuthId
    defaultLayout [whamlet|
&lt;p&gt;Note: Log in as "admin" to be an administrator.
&lt;p&gt;Your current auth ID: #{show maid}
$maybe _ &lt;- maid
    &lt;p&gt;
        &lt;a href=@{AuthR LogoutR}&gt;Logout
&lt;p&gt;
    &lt;a href=@{AdminR}&gt;Go to admin page
&lt;form method=post&gt;
    Make a change (admins only)
    \ #
    &lt;input type=submit&gt;
|]

postRootR :: Handler ()
postRootR = do
    setMessage "You made some change to the page"
    redirect RootR

getAdminR :: Handler RepHtml
getAdminR = defaultLayout [whamlet|
&lt;p&gt;I guess you're an admin!
&lt;p&gt;
    &lt;a href=@{RootR}&gt;Return to homepage
|]

main :: IO ()
main = do
    manager &lt;- newManager def
    warpDebug 3000 $ MyAuthSite manager</code></pre>
  <p id="x-6" class="hascomments">authRoute should be your login page, almost always
                AuthR
            LoginR. isAuthorized is a function that takes two
            parameters: the requested route, and whether or not the request was a "write" request.
            You can actually change the meaning of what a write request is using the
                isWriteRequest method, but the out-of-the-box version follows
            RESTful principles: anything but a GET, HEAD,
                OPTIONS or TRACE request is a write request.</p>
  <p id="x-7" class="hascomments">What's convenient about the body of isAuthorized is that you can run
            any Handler code you want. This means you can:</p>
  <ul id="ul_tew_ill_pe">
   <li id="x-8">Access the filesystem (normal IO)</li>
   <li id="x-9">Lookup values in the database</li>
   <li id="x-10">Pull any session or request values you want</li>
  </ul>
  <p id="x-11" class="hascomments">Using these techniques, you can develop as sophisticated an authorization system as you like,
   or even tie into existing systems used by your organization.</p>
 <h2>Conclusion</h2>
  <p id="x-3" class="hascomments">This chapter covered the basics of setting up user authentication, as well as how the built-in
   authorization functions provide a simple, declarative approach for users. While these are
   complicated concepts, with many approaches, Yesod should provide you with the building blocks you
   need to create your own customized auth solution.</p>
 </html>